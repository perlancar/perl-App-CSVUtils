#!perl

use 5.010001;
use strict;
use warnings;

use Getopt::Long qw(:config gnu_getopt no_ignore_case);
use Text::CSV_XS;

# AUTHORITY
# DATE
# DIST
# VERSION

binmode STDIN, ':encoding(utf8)';
binmode STDOUT, ':encoding(utf8)';

my %Opt = (
    header => 1,
);
GetOptions(
    'no-header' => sub { $Opt{header} = 0 },
);

my $csv = Text::CSV_XS->new({binary=>1});
my $i = 0;
my $header_row;
while (my $row = $csv->getline(\*ARGV)) {
    if (++$i == 1) {
        if ($Opt{header}) {
            $header_row = $row;
            for (@$row) { s/:/_/g }
            next;
        } else {
            $header_row = [map { "field".($_+1) } 0..$#{$row}];
        }
    }
    print join("\t", map { "$header_row->[$_]:$row->[$_]" } 0..$#{$row}), "\n";
}

# ABSTRACT: Convert CSV to LTSV
# PODNAME:

=head1 SYNOPSIS

 % csv2ltsv INPUT.csv > OUTPUT.ltsv


=head1 DESCRIPTION

B<Common notes for the utilities:>

Encoding: The utilities in this module/distribution accept and emit UTF8 text.


=head1 SEE ALSO

L<ltsv2csv>

Other CSV-related utilities in L<App::CSVUtils>.

Other LTSV-related utilities in L<App::LTSVUtils>.
